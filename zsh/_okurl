#compdef okurl
#autoload

_okurl_caching_policy() {
    # rebuild if cache is more than a day old
    local -a oldp
    oldp=( "$1"(mm+5) )
    (( $#oldp ))
}

_okurl_urls() {
  if [[ "$words[CURRENT]" =~ ^https://([^/]*)/.*$ ]]; then
      host=$match[1]
      if _cache_invalid _okurl-complete-$host || ! _retrieve_cache _okurl-complete-$host; then
          urls=($(okurl --urlCompletion "https://$host/"))
          _store_cache _okurl-complete-$host urls
      fi
      compadd $urls
  else
      if _cache_invalid _okurl-complete || ! _retrieve_cache _okurl-complete; then
          urls=($(okurl --urlCompletion https))
          _store_cache _okurl-complete urls
      fi
      compadd $urls
  fi
}

_okurl() {
  local curcontext="$curcontext" state state_descr line expl
  typeset -A opt_args

  zstyle -s ":completion:$curcontext:" cache-policy _okurl_caching_policy
  #zstyle -s ":completion:$curcontext:" use-cache true

  _arguments -C -S \
    --user-agent='[User-Agent to send to server]' \
    --connect-timeout='[Maximum time allowed for connection (seconds). (0 = disabled)]' \
    --read-timeout='[Maximum time allowed for reading data (seconds). (0 = disabled)]' \
    --ping-interval='[Interval between pings. (0 = disabled)]' \
    --frames'[Log HTTP/2 frames to STDERR]' \
    --debug'[Debug]' \
    --cache='[Cache directory]' \
    --protocols='[Protocols]' \
    --tracing='[Activate Zipkin Tracing]' \
    '--ip=[IP Preferences (system, ipv4, ipv6, ipv4only, ipv6only)]: :(system ipv4 ipv6 ipv4only ipv6only)' \
    '--dns=[DNS (netty, java, dnsoverhttps)]: :(netty java dnsoverhttps)' \
    '--dnsServers=[Specific DNS Servers (csv, google)]: :(csv google)' \
    --resolve='[DNS Overrides (HOST:TARGET)]' \
    --certificatePin='[Certificate Pin to define host:pinsha]' \
    '--networkInterface=[Specific Local Network Interface]: :->networkInterface' \
    '--connectionSpec=[Connection Spec (RESTRICTED_TLS, MODERN_TLS, COMPATIBLE_TLS)]: :(RESTRICTED_TLS MODERN_TLS COMPATIBLE_TLS)' \
    --cipherSuite='[Cipher Suites]' \
    --tlsVersions='[TLS Versions]'\
    --proxy='[Use HTTP proxy]' \
    --os-proxy'[Use OS defined proxy]' \
    --ssldebug'[SSL Debug]' \
    --user='[user:password for basic auth]' \
    --maxrequests='[Concurrency Level]' \
    --curl'[Show curl commands]' \
    --noFollow'[Follow redirects]' \
    --referer='[Referer URL]' \
    --authorize'[Authorize API]' \
    --renew'[Renew API Authorization]' \
    --remove'[Remove API Authorization]' \
    --token='[Use existing Token for authorization]' \
    --showCredentials'[Show Credentials]' \
    --complete='[Complete options]' \
    --urlCompletion'[URL Completion]' \
    --apidoc'[API Documentation]' \
    {-o,--output}='[Output file]' \
    {-k,--insecure}'[Allow connections to SSL sites without certs]' \
    {-i,--include}'[Include protocol headers in the output]' \
    {-s,--set}='[Token Set e.g. work]' \
    {-r,--raw}'[Raw Output]' \
    {-X,--request}='[Specify request command to use]' \
    {-d,--data}='[HTTP POST data]' \
    {-H,--header}='[Custom header to pass to server]' \
    {-h,--help}'[Show this help message and exit.]' \
    {-V,--version}'[Print version information and exit.]' \
    '*:URLs:->urls' \
    && ret=0

   case $state in
     urls)
        _okurl_urls
        ;;
   esac
}

_okurl "$@"
